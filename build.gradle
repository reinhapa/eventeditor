import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'signing'
    id 'application'
    id 'jacoco'
    id 'maven-publish'
    id 'com.github.ben-manes.versions' version '0.50.0'
    id 'com.google.osdetector' version '1.7.3'
    id 'net.nemerosa.versioning' version '3.0.0'
    id 'org.owasp.dependencycheck' version '8.4.3'
}

configurations {
    asciidoclet
}

group = 'net.reini'
version = '1.0-SNAPSHOT'

description = "JavaFX editor for Hackergarten events event.json file"

java {
    sourceCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

Date buildTimeAndDate = new Date()
ext {
    buildDate = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
    platform = osdetector.os == 'osx' ? 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os
}

dependencies {
    implementation 'org.glassfish.jersey.core:jersey-client:3.1.3'
    implementation 'org.glassfish.jersey.media:jersey-media-json-jackson:3.1.3'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.16.0'
    implementation 'org.controlsfx:controlsfx:11.2.0'

    // javaFX
    implementation "org.openjfx:javafx-base:18.+:$platform"
    implementation "org.openjfx:javafx-graphics:18.+:$platform"
    implementation "org.openjfx:javafx-fxml:18.+:$platform"
    implementation "org.openjfx:javafx-controls:18.+:$platform"

    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.7.0'

    testRuntimeOnly 'org.junit.platform:junit-platform-runner:1.10.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
    testRuntimeOnly 'org.mockito:mockito-inline:5.2.0'
}

signing {
    required { project.hasProperty('signing.password') && !project.version.contains('-SNAPSHOT') }
    sign configurations.archives
}

mainClassName = 'io.hackergarten.eventeditor.EventEditor'

jar {
    manifest {
        attributes 'Implementation-Title': 'JavaFX editor for hackergarten events',
                   'Implementation-Version': version,
                   'Created-By': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString(),
                   'Build-Date': buildDate,
                   'Build-Time': buildTime,
                   'Build-Revision': versioning.info.commit,
                   'Specification-Title': project.name,
                   'Specification-Version': project.version,
                   'Specification-Vendor': 'reini.net'
        metaInf {
            from file('.')
            include 'LICENSE'
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events 'skipped'
    }
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

check.dependsOn jacocoTestReport //, dependencyCheckUpdate, dependencyCheckAnalyze
